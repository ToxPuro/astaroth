## Options
option(BUILD_ACC_RUNTIME_LIBRARY "Builds the standalone acc runtime library" OFF)
option(RUNTIME_COMPILATION "Compile version of Astaroth compiled at runtime" OFF)
option(READ_OVERRIDES "Whether to read overrides generated by acc runtime compiler" OFF)
option(ELIMINATE_CONDITIONALS "Eliminates conditionals which are know to be or not to be taken at compilation time. Requires OPTIMIZE_MEM_ACCESSES=ON" OFF)
option(CPU_BUILD "To build CPU-only build" OFF)

if(RUNTIME_COMPILATION)
	set(RUNTIME_COMPILATION_VAL 1)
else()
	set(RUNTIME_COMPILATION_VAL 0)
endif()

if(CPU_BUILD)
	add_definitions(-DAC_CPU_BUILD=1)
else()
	add_definitions(-DAC_CPU_BUILD=0)
endif()

if(READ_OVERRIDES)
	set(READ_OVERRIDES_VAL 1)
else()
	set(READ_OVERRIDES_VAL 0)
endif()

if(ELIMINATE_CONDITIONALS)
	set(ELIMINATE_CONDITIONALS_VAL 1)
else()
	set(ELIMINATE_CONDITIONALS_VAL 0)
endif()

## DSL module dir

# NOTE: Manually defined DSL_MODULE_DIR must be set relative to the project root, not the actual
# build directory!
# NO! ../acc/mhd_solver
# YES! acc/mhd_solver
#


set(DSL_MODULE $ENV{DSL_MODULE})
if (DEFINED DSL_MODULE)
    set(DSL_MODULE_DIR ${PROJECT_SOURCE_DIR}/${DSL_MODULE})
endif ()

set(DSL_EXT_SOURCES $ENV{DSL_EXT_SOURCES})

if (NOT DEFINED DSL_MODULE_DIR)
    set(DSL_MODULE_DIR ${PROJECT_SOURCE_DIR}/samples/mhd_modular) # Default
else()
    get_filename_component(resolved_module_dir "${DSL_MODULE_DIR}" REALPATH
                           BASE_DIR ${CMAKE_BINARY_DIR})
    set(DSL_MODULE_DIR ${resolved_module_dir})
endif()

set(ACC_COMPILER_PATH $ENV{ACC_COMPILER_PATH})
if(DEFINED ACC_COMPILER_PATH)
	set(ACC_COMPILER ${ACC_COMPILER_PATH})
else()
	set(ACC_COMPILER ${PROJECT_BINARY_DIR}/acc/acc)
endif()



## Generate acc-runtime-headers
set(DSL_SOURCES "${DSL_MODULE_DIR}/*"
                "${PROJECT_SOURCE_DIR}/acc/stencilgen.c"
                "${PROJECT_SOURCE_DIR}/../src/core/stencil_accesses.cpp"
                "${PROJECT_SOURCE_DIR}/built-in/*"
		${DSL_EXT_SOURCES})

#"${PROJECT_SOURCE_DIR}/${EXT_SOURCES}")

set(DSL_HEADERS "user_kernels.h"
                "user_defines.h"
                "user_declarations.h"
                "stencil_accesses.h"
                "user_taskgraphs.h"
                "user_loaders.h"
                "stencilgen.h"
		"coeffs.h"
		)

set(DSL_MODULE_FILE $ENV{DSL_MODULE_FILE})
if (NOT DEFINED DSL_MODULE_FILE)
	set(ACC_COMPILER_ARGS 
		"${DSL_MODULE_DIR}/*.ac" 
		"${ELIMINATE_CONDITIONALS_VAL}" 
		"${READ_OVERRIDES_VAL}"  
		"${RUNTIME_COMPILATION_VAL}" 
		"${AC_BINARY_PATH}/overrides.h"
	)
else()
	set(ACC_COMPILER_ARGS 
		"${DSL_MODULE_DIR}/*.ac" 
		"${DSL_MODULE_FILE}"  
		"${ELIMINATE_CONDITIONALS_VAL}" 
		"${READ_OVERRIDES_VAL}"  
		"${RUNTIME_COMPILATION_VAL}" 
		"${AC_BINARY_PATH}/overrides.h"
	)
endif()
  add_custom_command (
      COMMENT "Building ACC objects: ${DSL_MODULE_DIR}"
      COMMAND  "${ACC_COMPILER}" ${ACC_COMPILER_ARGS}
      DEPENDS ${DSL_SOURCES} acc
      OUTPUT ${DSL_HEADERS}
  )
add_library(acc-runtime-headers INTERFACE ${DSL_HEADERS})
target_include_directories(acc-runtime-headers INTERFACE . .. ${CMAKE_CURRENT_BINARY_DIR})
target_compile_options(acc-runtime-headers INTERFACE -DIMPLEMENTATION=${IMPLEMENTATION})
target_compile_options(acc-runtime-headers INTERFACE -DMAX_THREADS_PER_BLOCK=${MAX_THREADS_PER_BLOCK})

add_library(acc-runtime-driver arrays.cc vba.cc common_kernels.cc)
target_link_libraries(acc-runtime-driver PUBLIC acc-runtime-headers ac_cuda_wrappers)
target_include_directories(acc-runtime-driver PUBLIC ${AC_BASE_PATH}/include)
target_include_directories(acc-runtime-driver PUBLIC ${AC_BASE_PATH}/src/core/kernels)

if (USE_HIP)
    target_link_libraries(acc-runtime-headers INTERFACE ${ROCTRACER})
    target_link_libraries(acc-runtime-headers INTERFACE ${HIPRAND})
    set_property(TARGET acc-runtime-headers PROPERTY HIP_ARCHITECTURES ${CMAKE_HIP_ARCHITECTURES})
else()
    set_property(TARGET acc-runtime-headers PROPERTY CUDA_ARCHITECTURES ${CMAKE_CUDA_ARCHITECTURES})
endif()

message("project source dir ${PROJECT_SOURCE_DIR}")

## Build the runtime library
if (BUILD_ACC_RUNTIME_LIBRARY)
	add_library(acc-runtime STATIC acc_runtime.cu ${AC_BASE_PATH}/src/core/helpers/helpers.cc)
    target_include_directories(acc-runtime PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/..)
    target_include_directories(acc-runtime PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/../../include)
    target_link_libraries(acc-runtime acc-runtime-headers acc-runtime-driver)

    if (CPU_BUILD)
	  set_source_files_properties(acc_runtime.cu PROPERTIES LANGUAGE CXX)
    else()
      if (USE_HIP)
          set_source_files_properties(acc_runtime.cu PROPERTIES LANGUAGE HIP)
          set_property(TARGET acc-runtime PROPERTY HIP_ARCHITECTURES ${CMAKE_HIP_ARCHITECTURES})
      else()
          set_property(TARGET acc-runtime PROPERTY CUDA_ARCHITECTURES ${CMAKE_CUDA_ARCHITECTURES})
      endif()
    endif()

endif()
